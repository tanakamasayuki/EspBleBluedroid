#include <EspBleBluedroid.h>

static constexpr const char *DEVICE_INFORMATION_SERVICE_UUID = "180a";
static constexpr const char *MANUFACTURER_NAME_UUID = "2a29";
static constexpr const char *MODEL_NUMBER_UUID = "2a24";
static constexpr const char *FIRMWARE_REVISION_UUID = "2a26";
static constexpr const char *PNP_ID_UUID = "2a50";

EspBleBluedroid bluetooth;

EspBleGattService deviceInformationServiceService;
EspBleGattCharacteristic manufacturerNameCharacteristic;
EspBleGattCharacteristic modelNumberCharacteristic;
EspBleGattCharacteristic firmwareRevisionCharacteristic;
EspBleGattCharacteristic pnpIdCharacteristic;
void setup()
{
  Serial.begin(115200);

  EspBleGattCharacteristicConfig readable;
  readable.readable = true;
  auto &server = bluetooth.gattServer();
  const uint8_t pnpId[] = {
    0x02,       // USB Implementers Forum vendor ID source
    0x34, 0x12, // Vendor ID 0x1234, little-endian
    0x78, 0x56, // Product ID 0x5678, little-endian
    0x00, 0x01  // Product version 0x0100, little-endian
  };
  if (!(deviceInformationServiceService = server.addService(DEVICE_INFORMATION_SERVICE_UUID)).valid() ||
      !(manufacturerNameCharacteristic = server.addCharacteristic(deviceInformationServiceService, MANUFACTURER_NAME_UUID, readable)).valid() ||
      !(modelNumberCharacteristic = server.addCharacteristic(deviceInformationServiceService, MODEL_NUMBER_UUID, readable)).valid() ||
      !(firmwareRevisionCharacteristic = server.addCharacteristic(deviceInformationServiceService, FIRMWARE_REVISION_UUID, readable)).valid() ||
      !(pnpIdCharacteristic = server.addCharacteristic(deviceInformationServiceService, PNP_ID_UUID, readable)).valid() ||
      !server.setValue(manufacturerNameCharacteristic, String("EspBleBluedroid")) ||
      !server.setValue(modelNumberCharacteristic, String("DeviceInfoServer")) ||
      !server.setValue(firmwareRevisionCharacteristic, String(ESPBLEBLUEDROID_VERSION_STR)) ||
      !server.setValue(pnpIdCharacteristic, pnpId, sizeof(pnpId)))
  {
    Serial.printf("Device Information configuration failed: %s\n",
      bluetooth.lastErrorDetail().c_str());
    return;
  }

  EspBleConfig config;
  config.deviceName = "Bluedroid Device Info";
  if (!bluetooth.begin(config))
  {
    Serial.printf("BLE initialization failed: %s\n", bluetooth.lastErrorDetail().c_str());
    return;
  }

  bluetooth.advertising().setName("Bluedroid Device Info");
  bluetooth.advertising().addServiceUuid(DEVICE_INFORMATION_SERVICE_UUID);
  bluetooth.advertising().start();
  Serial.println("Device Information Service is ready.");
}

void loop()
{
  bluetooth.update();
  delay(1);
}
